;-------------------------------------------------
;;カラオケを利用する
;-------------------------------------------------
@COM431
#DIM KARAOKE_CHARGE = 100
PRINTL
PRINTFORML 部屋代は1時間${KARAOKE_CHARGE}だ。
PRINTFORML 何時間利用しようか？
PRINTFORML (最大3時間、0でキャンセル)
INPUT
LOCAL=RESULT
IF LOCAL == 0 || LOCAL > 3
	RETURN 0
ELSE
	;所持金判定
	LOCAL:1 = LOCAL * KARAOKE_CHARGE
	IF MONEY < LOCAL:1
		PRINTL 所持金が足りない…
		RETURN 0
	ELSE
		;利用開始処理
		MONEY -= LOCAL:1
		CALL MANAGE_KARAOKE_TIME(LOCAL)
	ENDIF
ENDIF
RETURN 1

@COM_ABLE431
	;実行判定
	SIF !TFLAG:100
		RETURN 0
	;一括管理
	SIF GLOBAL_COMABLE(431)
		RETURN RESULT
	SIF CFLAG:MASTER:現在位置 != 場所_カラオケ
		RETURN 0
	SIF BASE:MASTER:気力 <= 0
		RETURN 0
RETURN 1

;-------------------------------------------------
;;曲を入れる
;-------------------------------------------------
@COM432
#DIM TARGET_保存
#DIM 好感度上昇
TARGET_保存=TARGET
FOR TARGET, 1, CHARA_ACTIVE
	IF CFLAG:TARGET:同行 != 0
		;うふふ中
		IF CFLAG:うふふ == 1
			;挿入中
			IF TEQUIP:50 == PLAYER || TEQUIP:51 == PLAYER
				SOURCE:快Ｖ = 400
				SOURCE:情愛 = 150
				SOURCE:苦痛 = 300
				SOURCE:露出 = 150
				SOURCE:露出 = 300
				SOURCE:反感 = 300
			;それ以外
			ELSE
				SOURCE:快Ｃ = 80
				SOURCE:快Ｂ = 80
				SOURCE:情愛 = 30
				SOURCE:性行動 = 60
				SOURCE:露出 = 50
				SOURCE:不潔 = 30
				SOURCE:逸脱 = 20
				SOURCE:反感 = 20
			ENDIF
		ELSE
			;実行判定
			SIF BASE:TARGET:体力 < 600 || BASE:TARGET:気力 < 500
				CONTINUE
			;固定で獲得するソース
			SOURCE:歓楽 = 400
			;ABL:従順をみる
			IF ABL:従順 <= 1
				SOURCE:歓楽 += (ABL:従順 * 60)
			ELSEIF ABL:従順 <= 3
				SOURCE:歓楽 += 200 + (ABL:従順 * 60)
			ELSEIF ABL:従順 <= 5
				SOURCE:歓楽 += 500 + (ABL:従順 * 60)
			ELSEIF ABL:従順 <= 8
				SOURCE:歓楽 += 750 + (ABL:従順 * 75)
			ELSEIF ABL:従順 <= 11
				SOURCE:歓楽 += 1000 + (ABL:従順 * 75)
			ELSE
				SOURCE:歓楽 += 2000 + (ABL:従順 * 30)
			ENDIF
			;好感度をみる
			IF CFLAG:好感度 <= 500
				SOURCE:歓楽 += CFLAG:2
			ELSEIF CFLAG:好感度 <= 5000
				SOURCE:歓楽 += 500 + (CFLAG:好感度 - 500) / 3
			ELSE
				SOURCE:歓楽 += 2000 + (CFLAG:好感度 - 5000) / 5
			ENDIF
			;態度をみる
			SIF GET_KARAOKE_ATTITUDE() <= 2
				TIMES SOURCE:歓楽, 0.50
			;同行人数に応じて減少
			LOCAL = GET_DOUKOU_NUM()
				SOURCE:歓楽 = SOURCE:歓楽 / LOCAL
			;調整
			SIF SOURCE:歓楽 < 0
				SOURCE:歓楽 = 0
			;ボーカル経験の取得
			IF GET_KARAOKE_ATTITUDE() > 1
				;同行人数が多いと減少
				IF LOCAL > 3
					LOCAL = 3
				ENDIF
				EXP:TARGET:ボーカル経験 += 1 + RAND:(5 - LOCAL)
			ENDIF
			;体力気力の減少
			BASE:TARGET:体力 -= 80
			BASE:TARGET:気力 -= 100
		ENDIF
	ENDIF
NEXT
TARGET=TARGET_保存
;実行判定
IF BASE:MASTER:体力 >= 600
	BASE:MASTER:体力 -= 80
	BASE:MASTER:気力 -= 100
ENDIF
TIME += 30
RETURN 1

@COM_ABLE432
	;実行判定
	SIF !TFLAG:100
		RETURN 0
	;一括管理
	;うふふ中は実行可とする
	SIF GLOBAL_COMABLE(432) && CFLAG:うふふ != 1
		RETURN RESULT
	SIF CFLAG:MASTER:現在位置 != 場所_カラオケあなた用
		RETURN 0
	SIF BASE:MASTER:気力 <= 0
		RETURN 0
RETURN 1

@MESSAGE_COM432
#DIM TARGET_保存
#DIM TFLAG_97_保存
TARGET_保存=TARGET
LOCAL:1 = 0
FOR TARGET, 1, CHARA_ACTIVE
	IF CFLAG:TARGET:同行 != 0
		LOCAL:1 += 1
		;うふふ中
		IF CFLAG:うふふ == 1
			;挿入中
			IF TEQUIP:50 == PLAYER || TEQUIP:51 == PLAYER
				PRINTFORML %CALLNAME:TARGET%とつながったまま、%CALLNAME:TARGET%に曲を歌わせた…
				PRINTFORML %CALLNAME:MASTER%に激しく突かれ、%CALLNAME:TARGET%の歌声はもはや歌になっていない…
			;それ以外
			ELSE
				PRINTFORML 責める手を休めないまま、%CALLNAME:TARGET%に曲を歌わせた…
				PRINTFORML %CALLNAME:MASTER%の愛撫に合わせて%CALLNAME:TARGET%の歌に甘い声が交じる…
			ENDIF
		;親密低い
		ELSE
			;実行判定
			IF BASE:TARGET:体力 < 600 || BASE:TARGET:気力 < 500
				PRINTFORML %CALLNAME:TARGET%は休んでいる。
				CONTINUE
			ENDIF
			SELECTCASE GET_KARAOKE_ATTITUDE()
			CASE 1
				PRINTFORM %CALLNAME:TARGET%は
				SELECTCASE RAND:2
					CASE 1
						PRINTL 退屈そうに座っている…
					CASEELSE
						PRINTL スマホを眺めている…
				ENDSELECT
			;臆病、プライド高い、恥じらいのいずれか
			CASE 2
				PRINTFORM %CALLNAME:TARGET%は
				SELECTCASE RAND:3
					CASE 2
						PRINT もじもじと
					CASE 1
						PRINT 恥ずかしそうに
					CASEELSE
						PRINT きょろきょろしながら
				ENDSELECT
				PRINTL 歌っている。
			CASE 3
				PRINTFORM %CALLNAME:TARGET%は
				SELECTCASE RAND:3
					CASE 2
						PRINT 真剣に
					CASE 1
						PRINT 楽しそうに
					CASEELSE
						PRINT ノリノリで
				ENDSELECT
				PRINTL 歌っている。
			;恋慕
			CASE 4
				PRINTFORM %CALLNAME:TARGET%は
				SELECTCASE RAND:3
					CASE 2
						PRINTFORM %CALLNAME:MASTER%をじっと見つめながら
					CASE 1
						PRINTFORM %CALLNAME:MASTER%に目配せしながら
					CASEELSE
						PRINTFORM %CALLNAME:MASTER%にウィンクしながら
				ENDSELECT
				PRINTL 歌っている。
			CASEELSE
			ENDSELECT
		ENDIF
		TRYCALLFORM KOJO_MESSAGE_COM_K{TARGET}_432(TARGET)
		;同行者の好感度上昇
		CALL FAVOR_CALC_NOT_TARGET(TARGET, TARGET_保存)
		PRINTW
	ENDIF
NEXT
;同行者がいない場合（ヒトカラ）
IF LOCAL:1 == 0
	;実行判定
	IF BASE:MASTER:体力 < 600
		PRINTW 体力が持たない...
	ELSE
		PRINTFORML ﾐﾐﾐﾝ!ﾐﾐﾐﾝ!ｳｰｻﾐﾝ!(*ﾟ∀ﾟ)o彡ﾟ
	ENDIF
ENDIF
TARGET=TARGET_保存


;-------------------------------------------------
;カラオケ利用処理
;引数が0以上の場合、開始処理を行う
;引数が0の場合、ターン終了時処理を行う
@MANAGE_KARAOKE_TIME(ARG)
#DIM KARAOKE_END_TIME
IF ARG > 0
	;終了時間を取得
	KARAOKE_END_TIME = DATETIME() + ARG * 60
	;プレイヤーと同行者を移動させる
	CFLAG:MASTER:現在位置 = 場所_カラオケあなた用
	FOR LOCAL, 1, CHARA_ACTIVE
		IF CFLAG:LOCAL:同行 != 0
			CFLAG:LOCAL:現在位置 = 場所_カラオケあなた用
		ENDIF
	NEXT
	;退室予定時刻を表示
	LOCAL = TIME + ARG * 60
	PRINTL
	PRINTFORMW 退室予定時刻は%TOSTR(LOCAL/60, "00")%:%TOSTR(LOCAL%60, "00")%です
ELSE
	;退室予定時間を過ぎている場合
	IF KARAOKE_END_TIME <= DATETIME()
		PRINTL
		PRINTFORML 店員から電話が掛かってきた
		PRINTFORMW どうやら退室時刻になったようだ…
		PRINTL
		PRINTFORMW %CALLNAME:MASTER%は個室を後にした
		;うふふ解除
		FOR LOCAL, 0, CHARA_ACTIVE
			CFLAG:LOCAL:うふふ = 0
			CFLAG:LOCAL:物陰 = 0
		NEXT
		;移動
		CFLAG:MASTER:現在位置 = 場所_カラオケ
		TIME +=10
	ENDIF
ENDIF
RETURN 1

;-------------------------------------------------
;同行者の人数を取得する
@GET_DOUKOU_NUM()
#FUNCTION
LOCAL:1 = 0
FOR LOCAL, 1, CHARA_ACTIVE
	IF CFLAG:LOCAL:同行 != 0
		LOCAL:1 += 1
	ENDIF
NEXT
RETURNF LOCAL:1

;態度を判定
;悪い 1 ⇔ 4 良い
@GET_KARAOKE_ATTITUDE
#FUNCTION
;恋慕
IF TALENT:恋慕
	RETURNF 4
;陽気、目立ちたがり、恥薄いのいずれか
ELSEIF TALENT:陽気／陰気 > 0 || TALENT:目立ちたがり || TALENT:羞恥心 < 0
	RETURNF 3
;親密低い
ELSEIF ABL:親密 <= 1
	RETURNF 1
;臆病、プライド高い、恥じらいのいずれか
ELSEIF TALENT:度胸 < 0 || TALENT:プライド > 0 || TALENT:羞恥心 > 0
	RETURNF 2
;上記以外
ELSE
	RETURNF 3
ENDIF
RETURNF 0

;-------------------------------------------------
;調教対象以外のキャラクター好感度上昇
;第１引数：上昇対象
;第２引数：調教対象
@FAVOR_CALC_NOT_TARGET(ARG, ARG:1)
#DIM TFLAG_97_保存
;好感度が累積されてしまうため、TFLAG:97を退避
TFLAG_97_保存 = TFLAG:97
IF ARG != ARG:1
	TFLAG:97 = 0
	PRINTFORM 　(%CALLNAME:ARG%) 
	CALL FAVOR_CALC(ARG)
ELSE
	PRINTL
ENDIF
TFLAG:97 = TFLAG_97_保存
