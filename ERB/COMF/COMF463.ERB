;-------------------------------------------------
;訪問者を誘う
;-------------------------------------------------
@COM463
	#DIM LOOP_CHR
	#DIM MSG表示フラグ
	PRINTFORMW %NTR_NAME(他人)%の姿を見つけ、%CALLNAME:MASTER%は%NTR_NAME(他人)%に近づき声をかけた
	; ここから他の住人が居る場合の処理
	MSG表示フラグ = 0
	FOR LOOP_CHR,1,CHARA_ACTIVE
		IF CFLAG:MASTER:現在位置 == CFLAG:LOOP_CHR:現在位置
			PRINT 部屋に来ないかと
			; 訪問者には懐いて(?)なく、主人公が好きな状態だと無理矢理言わされてると勘違いして主人公を救出
			IF CFLAG:LOOP_CHR:屈服度 <= 400 && (TALENT:LOOP_CHR:恋慕 || TALENT:LOOP_CHR:親愛)
				PRINTFORMW 誘おうとすると、%CALLNAME:LOOP_CHR%はそれを止め、%CALLNAME:MASTER%の手を掴んでその場から逃げ去った
				TRYCALLLIST
					FUNC KOJO_MESSAGE_COM_K{LOOP_CHR}_463_0(LOOP_CHR)
					FUNC KOJO_MESSAGE_COM_KU_463_0(LOOP_CHR)
				ENDFUNC
				CFLAG:MASTER:現在位置 = 場所_あなたの家
				CFLAG:LOOP_CHR:現在位置 = 場所_あなたの家
				RETURN 0
			; 訪問者に懐いて(?)なく、主人公ともあまり親しくない場合は逃げる
			ELSEIF CFLAG:LOOP_CHR:屈服度 <= 400
				PRINTFORMW 誘うと%NTR_NAME(他人)%は%CALLNAME:LOOP_CHR%も交えて三人で楽しまないかと持ちかけてきた
				TRYCALLLIST
					FUNC KOJO_MESSAGE_COM_K{LOOP_CHR}_463_1(LOOP_CHR)
					FUNC KOJO_MESSAGE_COM_KU_463_1(LOOP_CHR)
				ENDFUNC
				PRINTFORMW しかし、%CALLNAME:LOOP_CHR%はその言葉を聞いてそそくさとその場を立ち去ってしまった
				PRINTFORMW %CALLNAME:MASTER%が改めて二人きりじゃダメかなと聞くと%NTR_NAME(他人)%はそんなことはないと否定し、二人で部屋へと向かった
				MSG表示フラグ = 1
				BREAK
			; 訪問者に懐いて(?)いて、主人公も好きな場合は自ら乗り気に. 断ると嫉妬とかありかもしれない
			ELSEIF TALENT:LOOP_CHR:恋慕 || TALENT:LOOP_CHR:親愛
				PRINTFORMW 誘うと、%CALLNAME:LOOP_CHR%は自分も混ざりたいと３Ｐを持ちかけてきた
				TRYCALLLIST
					FUNC KOJO_MESSAGE_COM_K{LOOP_CHR}_463_2(LOOP_CHR)
					FUNC KOJO_MESSAGE_COM_KU_463_2(LOOP_CHR)
				ENDFUNC
				PRINTL [0] 三人で　[1] 二人だけがいい
				$INPUT_LOOP_COMF463_00
				INPUT
				IF RESULT == 0
					PRINTFORMW %NTR_NAME(他人)%の方を見ると、%NTR_NAME(他人)%はそれを了承し、三人で部屋に向かった
					TRYCALLLIST
						FUNC KOJO_MESSAGE_COM_K{LOOP_CHR}_463_3(LOOP_CHR)
						FUNC KOJO_MESSAGE_COM_KU_463_3(LOOP_CHR)
					ENDFUNC
					CFLAG:MASTER:現在位置 = 場所_あなたの家
					CFLAG:LOOP_CHR:現在位置 = 場所_あなたの家
					FLAG:訪問者の現在位置 = 場所_あなたの家
					CALL NTR_MASTER_3P_SEX(LOOP_CHR)
					RETURN 1
				ELSEIF RESULT == 1
					PRINTFORMW 二人きりがいいと%CALLNAME:LOOP_CHR%の提案を断ると、渋々ながらも了解してくれた
					TRYCALLLIST
						FUNC KOJO_MESSAGE_COM_K{LOOP_CHR}_463_4(LOOP_CHR)
						FUNC KOJO_MESSAGE_COM_KU_463_4(LOOP_CHR)
					ENDFUNC
					;TALENT:LOOP_CHR:嫉妬 = 1
					MSG表示フラグ = 1
					BREAK
				ELSE
					GOTO INPUT_LOOP_COMF463_00
				ENDIF
			; 訪問者に懐いて(?)いて、主人公とはあまり親しくない場合はむしろ訪問者を取る方向で
			ELSE
				PRINTFORMW 誘おうとすると、%CALLNAME:LOOP_CHR%はそれを阻むように%NTR_NAME(他人)%に話しかけた
				TRYCALLLIST
					FUNC KOJO_MESSAGE_COM_K{LOOP_CHR}_463_5(LOOP_CHR)
					FUNC KOJO_MESSAGE_COM_KU_463_5(LOOP_CHR)
				ENDFUNC
				PRINTFORMW タイミングを失ってしまった……
				RETURN 0
			ENDIF
		ENDIF
	NEXT
	SIF MSG表示フラグ == 0
		PRINTFORMW 部屋に来ないかと誘うと%NTR_NAME(他人)%はそれに頷き、二人で部屋へ向かった
	PRINTFORML 
	CFLAG:MASTER:現在位置 = 場所_あなたの家
	FLAG:訪問者の現在位置 = 場所_あなたの家

	PRINTFORM 部屋に到着すると%NTR_NAME(他人)%は%CALLNAME:MASTER%の
	SIF DRESS_LEVEL(MASTER) & (1p4)
		PRINT 服を手早く脱がし、
	SIF DRESS_LEVEL(MASTER) & (1p2)
		PRINT 下着を取り去り、
	PRINTW 首筋や唇にキスをしながらベッドに押し倒した
	PRINTFORM そして、%CALLNAME:MASTER%の足を抱えて丸見えになった%MSG_SLIT(MASTER)%にペニスをあてがい
	IF TALENT:MASTER:処女
		PRINTW 亀頭を浅く挿入してから、処女膜を引き裂きながら一つになっていった
	ELSE
		PRINTW ゆっくりと貫いて一つになった
	ENDIF
	CALL CLOTHES_Preset_NUDE(MASTER)
	LOCAL = RAND:20 + 10
	TIME += LOCAL
	PRINTFORMW 　　　　　　・
	PRINTFORMW 　　　　　　・
	PRINTFORMW 　　　　　　・
	IF TALENT:MASTER:処女
		PRINTFORML %CALLNAME:MASTER%の処女は%NTR_NAME(他人)%に捧げた
		PRINTFORML ＜%CALLNAME:MASTER%は処女を失いました＞
	ENDIF
	PRINTFORML {LOCAL}分間%CALLNAME:MASTER%の膣内を味わった後、%NTR_NAME(他人)%は子宮に精液を注ぎ込んだ
	PRINTFORMW 溢れ出た精液\@ TALENT:MASTER:処女 ? と破瓜の血 # \@は\@ BEDROOM(FLAG:訪問者の現在位置) ? シーツ # 地面 \@に零れ、染み込んでいった
	CALL NTR_MASTER_SEX_STATUS(0)
	IF TALENT:MASTER:処女
		FLAG:1840 = 3
	ELSE
		FLAG:1840 = 4
	ENDIF

