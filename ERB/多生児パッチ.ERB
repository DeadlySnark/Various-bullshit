;多生児出産(ryパッチ======================================
;新規追加フラグ
;FLAG:53 多生児出産と(ryパッチVerUP用フラグ
;FLAG:54 訪問者の子供の数

;CFLAG:85 子供の人数
;CFLAG:86 父親の何人目の子供(子供用フラグ)
;CFLAG:87 母親の何人目の子供(子供用フラグ)
;CFLAG:88 里子に出した数

;CFLAG:353 何人出産するか(出産後は育児中の人数となる)
;CFLAG:354 多生児の兄弟用フラグ


;====================================================
;SHOP.ERB〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜



;PREGNACY_S.ERB〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜

;出産直前に処理---------------------------------
@生まれる人数, ARG
;産まれる人数確認
CFLAG:ARG:出産数 = 1
;獣系(素質[猫舌][動物耳][狐][妖狐]のいずれかを持つ)は多生児を産みやすい
IF TALENT:ARG:猫舌 || TALENT:ARG:動物耳 || TALENT:ARG:繁殖母体
	SIF RAND:多生児出産強度(ARG, 80) == 0
		CFLAG:ARG:出産数 = 2
	SIF RAND:多生児出産強度(ARG, 120) == 0
		CFLAG:ARG:出産数 = 3
	SIF RAND:多生児出産強度(ARG, 160) == 0
		CFLAG:ARG:出産数 = 4
	SIF RAND:多生児出産強度(ARG, 260) == 0
		CFLAG:ARG:出産数 = 5
	SIF RAND:多生児出産強度(ARG, 500) == 0
		CFLAG:ARG:出産数 = 6
	SIF RAND:多生児出産強度(ARG, 650) == 0
		CFLAG:ARG:出産数 = 7
	SIF RAND:多生児出産強度(ARG, 800) == 0
		CFLAG:ARG:出産数 = 8
ELSE
	SIF RAND:多生児出産強度(ARG, 100) == 0
		CFLAG:ARG:出産数 = 2
	SIF RAND:多生児出産強度(ARG, 250) == 0
		CFLAG:ARG:出産数 = 3
	SIF RAND:多生児出産強度(ARG, 500) == 0
		CFLAG:ARG:出産数 = 4
	SIF RAND:多生児出産強度(ARG, 750) == 0
		CFLAG:ARG:出産数 = 5
	SIF RAND:多生児出産強度(ARG, 1000) == 0
		CFLAG:ARG:出産数 = 6
	SIF RAND:多生児出産強度(ARG, 2000) == 0
		CFLAG:ARG:出産数 = 7
	SIF RAND:多生児出産強度(ARG, 3000) == 0
		CFLAG:ARG:出産数 = 8
ENDIF
IF TALENT:ARG:繁殖母体==2
;真・繁殖母体
;限界突破し、もっと生ませる(多分20近くになる)
	CFLAG:ARG:出産数 += 正規分布(2*CFLAG:ARG:出産数)
ENDIF
TRYCALL 生まれる人数DEBUG, ARG

@親子関係追加取得, ARG, ARG:1, ARG:2
;ARG   子供
;ARG:1 ARGの父
;ARG:2 ARGの母

;父母の子供の人数フラグ設定
;父親が訪問者なら訪問者用の人数フラグ処理
IF ARG:1 >= CHARA_ACTIVE
	FLAG:訪問者の子供の数 ++
ELSE
	CFLAG:(ARG:1):85 ++
ENDIF
CFLAG:(ARG:2):85 ++

;子供が父母の何人目の子供かを設定
IF ARG:1 >= CHARA_ACTIVE
	CFLAG:ARG:父親の子番号 = FLAG:訪問者の子供の数
ELSE
	CFLAG:ARG:父親の子番号 = CFLAG:(ARG:1):85
ENDIF
CFLAG:ARG:母親の子番号 = CFLAG:(ARG:2):85

;母親及び父親がMASTERではない時の出産口上---------------------
@両親がMASTER以外の出産口上, ARG, ARG:1
;ARG   母親の番号
;ARG:1 子供の番号

PRINTFORMW %CALLNAME:ARG%が%N_T_R(CFLAG:(ARG:1):73, 1)%の子を\@ CFLAG:ARG:出産数 != 1 ?{CFLAG:ARG:出産数}人#\@産みました。



;生まれた人数に応じて口上変化---------------------
@多生児出産口上, ARG, ARG:1
;設定上八つ子まで
SELECTCASE ARG
	CASE 1
		PRINT
	CASE 2
		PRINT 双
	CASE 3
		PRINT 三つ
	CASE 4
		PRINT 四つ
	CASE 5
		PRINT 五つ
	CASE 6
		PRINT 六つ
	CASE 7
		PRINT 七つ
	CASE 8
		PRINT 八つ
	CASEELSE
		PRINTFORM {ARG}つ
ENDSELECT
PRINT 子
SIF ARG:1 == 0
	PRINTW の赤ちゃんだ。
;SIF ARG:1 == 1
;	PRINT の子

@多生児フラグ処理, ARG, ARG:1
;ARG 生まれる子供
;ARG:1 現在の繰り返し番号

;一番目の子
IF ARG:1 == 0
	LOCAL:1 = 0
	;他の子供をチェックし、CFLAG:354の最大数を取得
	FOR LOCAL, 0, CHARA_ACTIVE
		SIF NO:LOCAL != 501 && NO:LOCAL != 502
			CONTINUE
		SIF LOCAL:1 < CFLAG:LOCAL:多生児兄弟フラグ
			LOCAL:1 = CFLAG:LOCAL:多生児兄弟フラグ
	NEXT

	CFLAG:ARG:多生児兄弟フラグ = (LOCAL:1 + 1)
ELSE
	CFLAG:ARG:多生児兄弟フラグ = CFLAG:(CHARA_ACTIVE-2):多生児兄弟フラグ
	
ENDIF


@里子, ARG
;生まれる子供の数だけ追加
SIF (CFLAG:ARG:にんっしんっ - 1) < CHARA_ACTIVE
	CFLAG:(CFLAG:ARG:にんっしんっ - 1):88 += CFLAG:ARG:出産数
CFLAG:ARG:里子人数 += CFLAG:ARG:出産数

SIF (CFLAG:ARG:にんっしんっ - 1) < CHARA_ACTIVE
	CFLAG:(CFLAG:ARG:にんっしんっ - 1):85 += CFLAG:ARG:出産数
CFLAG:ARG:子供の人数 += CFLAG:ARG:出産数




@子供相性設定, ARG
;ARG 相性を設定するキャラ

;父親が登録外のキャラの場合の処理
IF CFLAG:ARG:父親 >= CHARA_ACTIVE
	FOR LOCAL, 0, CHARA_ACTIVE
		IF RELATION:(CFLAG:ARG:母親):LOCAL == 0 || RELATION:(CFLAG:ARG:母親):LOCAL == 100
			RELATION:ARG:LOCAL = 100
		ELSE
			;受け継ぐ相性が100超なら ((受け継ぐ相性-100)/2)+100
			;受け継ぐ相性が100未満なら((100-受け継ぐ相性)/2)+受け継ぐ相性
			IF RELATION:(CFLAG:ARG:母親):LOCAL > 100
				RELATION:ARG:LOCAL = ((RELATION:(CFLAG:ARG:母親):LOCAL - 100) / 2) + 100
			ELSEIF RELATION:(CFLAG:ARG:母親):LOCAL < 100
				RELATION:ARG:LOCAL = ((100 - RELATION:(CFLAG:ARG:母親):LOCAL) / 2) + RELATION:(CFLAG:ARG:母親):LOCAL
			ENDIF
		ENDIF
	NEXT
	RELATION:ARG:(CFLAG:ARG:母親) = 300
ELSE
	FOR LOCAL, 0, CHARA_ACTIVE
		IF (RELATION:(CFLAG:ARG:母親):LOCAL == 0 || RELATION:(CFLAG:ARG:母親):LOCAL == 100) && (RELATION:(CFLAG:ARG:父親):LOCAL == 0 || RELATION:(CFLAG:ARG:父親):LOCAL == 100)
			RELATION:ARG:LOCAL = 100
		ELSE
			;対象LOCALに対する相性が両親共に同じか
			IF RELATION:(CFLAG:ARG:母親):LOCAL == RELATION:(CFLAG:ARG:父親):LOCAL
				;受け継ぐ相性が100超なら ((受け継ぐ相性-100)/2)+100
				;受け継ぐ相性が100未満なら((100-受け継ぐ相性)/2)+受け継ぐ相性
				IF RELATION:(CFLAG:ARG:母親):LOCAL > 100
					RELATION:ARG:LOCAL = (((RELATION:(CFLAG:ARG:母親):LOCAL - 100) / 2) + 100)
				ELSEIF RELATION:(CFLAG:ARG:母親):LOCAL < 100
					RELATION:ARG:LOCAL = (((100 - RELATION:(CFLAG:ARG:母親):LOCAL) / 2) + RELATION:(CFLAG:ARG:母親):LOCAL)
				ENDIF
			ELSE
				;両親の平均値を取得
				RELATION:ARG:LOCAL = ((RELATION:(CFLAG:ARG:母親):LOCAL + RELATION:(CFLAG:ARG:父親):LOCAL) / 2)
			ENDIF
		ENDIF
	NEXT
	RELATION:ARG:(CFLAG:ARG:父親) = 300
	RELATION:ARG:(CFLAG:ARG:母親) = 300
ENDIF


;PRINT_STATE.ERB〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜
;-------------------------------------------------
;親の名前表示
;-------------------------------------------------
;親の名前をステータスに表示します
;使用変数一覧
;CFLAG:73 父親
;CFLAG:74 母親
;CFLAG:86 父親の第×子
;CFLAG:87 母親の第×子
@PRINT_PARENT(ARG)
;息子または娘でないとスキップ
SIF NO:ARG != 501 && NO:ARG != 502
	RETURN 0
VARSET LOCAL

IF CFLAG:ARG:父親 >= 人物_客
	PRINTFORM 父：不明
ELSEIF CFLAG:ARG:父親 == 人物_村人
	PRINTFORM 父：不明
ELSEIF CFLAG:ARG:父親 == 人物_訪問者
	PRINTFORM 父：%NTR_NAME(他人)% 
	PRINTFORM (第{CFLAG:ARG:父親の子番号}子) 
ELSE
	PRINTFORM 父：%CALLNAME:(CFLAG:ARG:73)%
	PRINTFORM (第{CFLAG:ARG:父親の子番号}子) 
ENDIF
;前二つはいらないけど一応つけておく
IF CFLAG:ARG:母親 >= 人物_客
	PRINTFORM 母：不明
ELSEIF CFLAG:ARG:母親 == 人物_村人
	PRINTFORM 母：不明
ELSEIF CFLAG:ARG:母親 == 人物_訪問者
	PRINTFORM 母：%NTR_NAME(他人)% 
	PRINTFORM (第{CFLAG:ARG:母親の子番号}子) 
ELSE
	PRINTFORM 母：%CALLNAME:(CFLAG:ARG:74)%
	PRINTFORM (第{CFLAG:ARG:母親の子番号}子)
ENDIF

;-------------------------------------------------
;子の名前表示
;-------------------------------------------------
;子の名前をステータスに表示します
;使用変数一覧
;LOCAL:1  父母判定
;CFLAG:73 父親
;CFLAG:74 母親
@PRINT_PARENT_2(ARG)
;一人もいないとスキップ
SIF CFLAG:ARG:子供の人数 == 0 && !TALENT:ARG:妊娠
	RETURN 0
VARSET LOCAL
;ARGの子供の名前を出す
IF CFLAG:ARG:子供の人数
	PRINTL 父親としての子供（()内は母親の名前）
	FOR LOCAL:1, 0, CHARA_ACTIVE
		IF (NO:(LOCAL:1) == 501 || NO:(LOCAL:1) == 502) && CFLAG:(LOCAL:1):73 == ARG
			PRINTFORM  %N_T_R(LOCAL:1)%(%N_T_R(CFLAG:(LOCAL:1):74)%)
			LOCAL += 1
			IF (LOCAL%3) == 0
				PRINTL 
			ELSE
				PRINT 　
			ENDIF
		ENDIF
	NEXT
	LOCAL = 0
	PRINTL 
	PRINTL 
	PRINTL 母親としての子供（()内は父親の名前）
	FOR LOCAL:1, 0, CHARA_ACTIVE
		IF (NO:(LOCAL:1) == 502 || NO:(LOCAL:1) == 501) && CFLAG:(LOCAL:1):74 == ARG
			PRINTFORM  %N_T_R(LOCAL:1)%(%N_T_R(CFLAG:(LOCAL:1):73)%)
			LOCAL += 1
			IF (LOCAL%3) == 0
				PRINTL 
			ELSE
				PRINT 　
			ENDIF
		ENDIF
	NEXT
	PRINTL
	PRINTL 
;	PRINTFORML  里子に出した子供の人数 {CFLAG:ARG:里子人数} 人
;	PRINTFORML 　　　　　　　　　　　　　　　　　　　　　　　計 {CFLAG:ARG:子供の人数} 人
	PRINTL
ENDIF

SIF TALENT:ARG:妊娠
	PRINTFORML 現在%N_T_R_P(CFLAG:ARG:にんっしんっ - 1)%の子供を身篭っている


;-------------------------------------------------
;兄弟の名前表示
;-------------------------------------------------
;兄弟の名前をステータスに表示します
;里子に出された兄弟は表示されません
;使用変数一覧
;LOCAL:1  兄弟判定
;LOCAL:2  兄弟数及び兄弟の番号
;LOCAL:3  多生児の場合の人数
;LOCAL:4  多生児の兄弟判定
;CFLAG:73 父親
;CFLAG:74 母親
;CFLAG:354 多生児判定
@PRINT_PARENT_3(ARG)
VARSET LOCAL

;初期キャラの兄弟関係
;IF ARG == 人物_E
;	SETCOLOR 色設定_兄弟_自分
;	PRINTFORM  1_%CALLNAME:人物_E%
;	RESETCOLOR
;	PRINTFORM  2_%CALLNAME:人物_フラン%
;ELSEIF ARG == 人物_フラン
;	PRINTFORM  1_%CALLNAME:人物_E%
;	SETCOLOR 色設定_兄弟_自分
;	PRINTFORM  2_%CALLNAME:人物_フラン%
;	RESETCOLOR
;ENDIF

;息子または娘でないとスキップ
SIF NO:ARG != 501 && NO:ARG != 502
	RETURN 0
FOR LOCAL:1, 0, CHARA_ACTIVE
	;父親、母親が同じ
	IF (NO:(LOCAL:1) == 501 || NO:(LOCAL:1) == 502) && (CFLAG:(LOCAL:1):73 == CFLAG:ARG:73 || CFLAG:(LOCAL:1):74 == CFLAG:ARG:74)
		LOCAL:2 ++
		SIF LOCAL:1 == ARG
			SETCOLOR 色設定_兄弟_自分
		PRINTFORM  {LOCAL:2}_%CALLNAME:(LOCAL:1)%
		RESETCOLOR
		IF CFLAG:ARG:多生児兄弟フラグ > 0 && CFLAG:(LOCAL:1):多生児兄弟フラグ == CFLAG:ARG:多生児兄弟フラグ
			IF LOCAL:3 == 0
				FOR LOCAL:4, 0, CHARA_ACTIVE
					SIF (NO:(LOCAL:4) == 502 || NO:(LOCAL:4) == 501) && CFLAG:ARG:多生児兄弟フラグ == CFLAG:(LOCAL:4):多生児兄弟フラグ
						LOCAL:3 ++
				NEXT
			ENDIF
			;多生児表記は対象だけ
			PRINT (
			CALL 多生児出産口上, LOCAL:3, 2
			PRINT )
				
		ELSEIF CFLAG:(LOCAL:1):73 != CFLAG:ARG:73
			PRINT (異父)
		ELSEIF CFLAG:(LOCAL:1):74 != CFLAG:ARG:74
			PRINT (異母)
		ENDIF

		LOCAL += 1
		IF (LOCAL%3) == 0
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
	ENDIF
NEXT
PRINTL


;-------------------------------------------------
;出産表
;-------------------------------------------------
;子の名前をステータスに表示します
;使用変数一覧
;LOCAL   父親
;LOCAL:1 スペース
;LOCAL:2 父親(名前用)
;LOCAL:3 累計数 後で子供の数と比較して、足らない分を表示
@PRINT_PARENT_4(ARG)
;一人もいないとスキップ

SIF CFLAG:ARG:子供の人数 == 0
	RETURN 0

VARSET LOCAL

LOCALS = 　　　　　　　　　　　　　　
STRLENS LOCALS
LOCAL:1 = RESULT

PRINTL 　父親　　　　　　　　　　　　産んだ人数
PRINTL
FOR LOCAL, 0, 1098
	LOCAL:2+=親別出産数:(NO:ARG):LOCAL
	;自分はskip
	SIF LOCAL == ARG
		CONTINUE
	;明らかに空の部分もskip
	SIF CSVキャラ数 < LOCAL && LOCAL < 人物_訪問者
		CONTINUE
	LOCALS:1 = %N_T_R_P(LOCAL),30,LEFT%
	;0でないときだけ表示
	SIF 親別出産数:(NO:ARG):LOCAL
		PRINTFORML 　%LOCALS:1% {親別出産数:(NO:ARG):LOCAL}
	
	
NEXT
SIF LOCAL:2!=CFLAG:ARG:子供の人数
	PRINTFORML 　不明 　　　　　　　　　　 　{-LOCAL:2+CFLAG:ARG:子供の人数}

;-------------------------------------------------
;キャラ同士の相性表示処理
;-------------------------------------------------
@PRINT_RELATION(ARG)
PRINTL
PRINTL □ 相性 □---------------------------------------------------------------------
TRYCALL 現在位置_開始位置DEBUG, ARG
PRINTFORML
LOCAL = 0
FOR LOCAL:1,0,CHARANUM
	IF RELATION:ARG:(NO:(LOCAL:1)) != 0 && RELATION:ARG:(NO:(LOCAL:1)) != 100
		;あなたダミー回避処理
		SIF NO:(LOCAL:1)==0 && LOCAL:1 != 0
			CONTINUE
		;v640 対して仲が良くもなくさりとて悪くもないキャラはスキップ
		SIF ABS(RELATION:ARG:(NO:(LOCAL:1))-100)<10
			CONTINUE
		PRINTFORM  %NAME:(LOCAL:1)%（{RELATION:ARG:(NO:(LOCAL:1))}）
		LOCAL += 1
		IF (LOCAL%3) == 0
			PRINTL 
		ELSE
			PRINT 　
		ENDIF
	ENDIF
NEXT
IF LOCAL == 0
	PRINTL 　　特になし
ELSEIF LOCAL%3 != 0
	PRINTL 
ENDIF


;その他いろいろ〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜
@N_T_R(ARG, ARG:1)
#FUNCTIONS
;ARG キャラ番号ないし訪問者(998)番号

;訪問者による第１引数に引っかかる時とかにいちいちIF文とか使わないでいけるようにするもの
;%N_T_R(LOCAL, 0)%

SELECTCASE ARG:1
	CASE 0
		SELECTCASE ARG
			CASE 0 TO CHARA_ACTIVE-1

					RETURNF CALLNAME:ARG
				RETURNF "エラー"
			CASE 人物_訪問者
				;RETURNF "訪問者"
				RETURNF NTR_NAME(0)
			CASE 人物_客
				RETURNF "客"
			CASE 人物_村人
				RETURNF "ファン"
			CASE 人物_風俗オーナー
				RETURNF "ヤクザ"
			CASE 人物_審査員
				RETURNF "審査員"
			CASEELSE
				RETURNF "名前が定義されていない何か"
		ENDSELECT
	CASE 1
		SELECTCASE ARG
			CASE 0 TO CHARA_ACTIVE-1
				RETURNF CALLNAME:ARG
			CASE 人物_訪問者
				;RETURNF "訪問者"
				RETURNF NTR_NAME(0)
			CASE 人物_風俗オーナー
				RETURNF "ヤクザ"
			CASE 人物_審査員
				RETURNF "審査員"
			CASE 人物_客, 人物_村人
				RETURNF "名も知らない誰か"
			CASE IS > 人物_客
				RETURNF "誰か"
			CASEELSE
				RETURNF "名前が定義されていない何か"
		ENDSELECT
ENDSELECT

;妊娠時に使用。NOで処理する
@N_T_R_P(ARG)
#FUNCTIONS
#DIMS 名前
;ARG キャラ番号ないし訪問者(998)番号

;訪問者による第１引数に引っかかる時とかにいちいちIF文とか使わないでいけるようにするもの
;%N_T_R(LOCAL, 0)%

		SELECTCASE ARG
			CASE 0 TO CSVキャラ数
				EXISTCSV ARG
				SIF RESULT
					RETURNF CSVCALLNAME(ARG)
				RETURNF "エラー"
			CASE 人物_客, 人物_村人
				RETURNF "名前も知らない男"
			CASE IS >= 10000
				RETURNF "誰か"
			CASE IS >= 人物_訪問者
				;RETURNF "訪問者"
				RETURNF NTR_NAME(ARG)
			CASEELSE
				RETURNF "名前が定義されていない何か"
		ENDSELECT


;多生児出産確率------------------
@多生児出産強度(ARG, ARG:1)
#FUNCTION
#DIM 強度
強度=MAX(0, CFLAG:ARG:多生児強度フラグ-3)
SELECTCASE 強度
	CASE 1
		;8つ子の確立、獣系でRAND:640 それ以外でRAND:2400となる
		TIMES ARG:1, 0.80
	CASE 2
		;8つ子の確立、獣系でRAND: それ以外でRAND:となる
		TIMES ARG:1, 0.50
	CASE 3
		;8つ子の確立、獣系でRAND: それ以外でRAND:となる
		TIMES ARG:1, 0.30
	CASE 4
		;8つ子の確立、獣系でRAND: それ以外でRAND:となる
		TIMES ARG:1, 0.15
	CASE 5
		;8つ子の確立、獣系でRAND:40 それ以外でRAND:150となる
		TIMES ARG:1, 0.05
	CASE 6
		;8つ子の確立、獣系でRAND:8 それ以外でRAND:30となる
		TIMES ARG:1, 0.01
	CASE 7
		TIMES ARG:1, 0.005
ENDSELECT

IF ARG:1 == 1
	ARG:1 = 2
ELSEIF ARG:1 == 0
	ARG:1 = 1
ENDIF

RETURNF ARG:1


