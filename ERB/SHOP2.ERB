;調教対象選択
@CHANGE_TARGET
	IF TARGET >= 0
		PRINT 現在
		PRINTFORML %NAME:TARGET%を調教中
	ENDIF
	PRINTL 今回は誰を調教しますか？
	CALL LIFE_LIST(1)
	PRINTFORML  [1000]戻る

	$INPUT_LOOP
	INPUT
	IF RESULT == 1000
		RETURN 0
	ELSEIF RESULT < 1 || RESULT >= CHARA_ACTIVE
		GOTO INPUT_LOOP
	ENDIF
	LOCAL = RESULT
	CALL PRINT_STATE(RESULT)
	CALL CHOICE("このキャラを調教しますか", "はい", "いいえ")
	SIF RESULT
		RESTART
	TARGET = LOCAL
	IF TARGET == ASSI
		ASSI = -1
		PRINTFORM 今回は助手の
	ENDIF
	PRINTFORMW %NAME:TARGET%を調教します。

@SELECT_ASSI
	PRINT 現在
	IF ASSI >= 0
		PRINTFORML [{ASSI}]%NAME:ASSI%が助手
	ELSE
		PRINTFORML 助手はいません
	ENDIF
	PRINTL 誰を助手にしますか？(現在の助手は☆)

	PRINTFORML   [ 0]助手なし
	CALL LIFE_LIST(2)
	PRINTFORML  [1000]戻る

	$INPUT_LOOP
	INPUT
	IF RESULT == 100
		RETURN 0
	ELSEIF RESULT == 0
		ASSI = -1
		RETURN 0
	ELSEIF RESULT < 0 || RESULT >= CHARA_ACTIVE
		GOTO INPUT_LOOP
	ELSEIF RESULT == MASTER
		GOTO INPUT_LOOP
	ELSEIF CFLAG:RESULT:既成事実 < 2
		GOTO INPUT_LOOP
	ENDIF
	ASSI = RESULT
	ISASSI:ASSI = 1
	SIF ASSI == TARGET
		TARGET = -1


@LIFE_LIST(ARG)
	;ARG = 0 ステータス表示
	;ARG = 1 調教対象選択
	;ARG = 2 助手選択
	;ARG = 3 キャラ売却
	#DIM LOOP_CHR
	#DIM LOOP_I
	#DIM LOOP_J
	#DIM 精子量
	DRAWLINE
	IF !ARG
		PRINTFORML 　[0]%NAME:MASTER%
		DRAWLINE
	ENDIF
	FOR LOOP_CHR,1, CHARA_ACTIVE
	;所属しているキャラのみ表示
	SIF !所属(LOOP_CHR)
		CONTINUE
		IF ARG == 2
			SIF !(CFLAG:LOOP_CHR:既成事実 & 2)
				CONTINUE
			IF COUNT == ASSI
				PRINT ☆
			ELSE
				PRINT 　
			ENDIF
		ENDIF
		IF ARG == 3
			SIF !(CFLAG:LOOP_CHR:既成事実 & 1)
				CONTINUE
		ENDIF
		PRINTFORM [{LOOP_CHR,2}]
		PRINTFORM %CALLNAME:LOOP_CHR,18,LEFT%

		SELECTCASE FLAG:1003
			;基礎表示選択
			CASE 2
				PRINTFORM \@(FLAG:1004) ? 気力#体力\@
				A = BASE:LOOP_CHR:(FLAG:1004)
				SIF BASE:LOOP_CHR:(FLAG:1004) < 0
					A = 0
				BAR A,MAXBASE:LOOP_CHR:(FLAG:1004),10
				PRINTFORM ({A,4}/{MAXBASE:LOOP_CHR:(FLAG:1004),4})
		
				SIF ARG == 0 && CFLAG:LOOP_CHR:乙女の秘密
					TRYCALL 簡易追加情報, LOOP_CHR
		
			;能力表示選択or性技表示選択
			CASE 3, 4
				IF FLAG:1003 == 4 && !(HAS_VAGINA(LOOP_CHR)) && (FLAG:1004 == 52 || FLAG:1004 == 54)
					PRINTFORM %ABLNAME:(FLAG:1004),12%:* 
				ELSE
					PRINTFORM %ABLNAME:(FLAG:1004),12%:{ABL:LOOP_CHR:(FLAG:1004),5,LEFT} 
				ENDIF
			;刻印表示選択
			CASE 5
				PRINTFORM %MARKNAME:(FLAG:1004),12%:LV{MARK:LOOP_CHR:(FLAG:1004),5,LEFT} 
			;経験表示選択
			CASE 6
				IF (!(HAS_VAGINA(LOOP_CHR)) && FLAG:1004 == 51) || (HAS_VAGINA(LOOP_CHR) && FLAG:1004 == 52)
					PRINTFORM %EXPNAME:(FLAG:1004),16%:* 
				ELSE
				PRINTFORM %EXPNAME:(FLAG:1004),16%:{EXP:LOOP_CHR:(FLAG:1004),5,LEFT} 
				ENDIF
			;宝珠表示選択
			CASE 7
				LOCALS = {JUEL:LOOP_CHR:(FLAG:1004)}
				SIF JUEL:LOOP_CHR:(FLAG:1004) > 1000000
					LOCALS = {JUEL:LOOP_CHR:(FLAG:1004) / 1000}k
				SIF JUEL:LOOP_CHR:(FLAG:1004) > 1000000000
					LOCALS = {JUEL:LOOP_CHR:(FLAG:1004) / 1000000}m
				PRINTFORM %PALAMNAME:(FLAG:1004),6%の珠:%LOCALS,7,LEFT%
			;その他表示選択
			CASE 8
				SELECTCASE FLAG:1004
					;既成事実
					CASE 1
						VARSET LOCALS
						SIF GETBIT(CFLAG:LOOP_CHR:(FLAG:1004),0) 
							LOCALS += "告白成功　"
						SIF GETBIT(CFLAG:LOOP_CHR:(FLAG:1004),1) 
							LOCALS += "うふふ経験あり　"
						PRINTFORM %LOCALS%
					;好感度
					CASE 2
						VARSET LOCALS
						;恋慕/NTR状態
						SIF TALENT:LOOP_CHR:NTR
							LOCALS += "NTR "
						SIF TALENT:LOOP_CHR:恋慕
							LOCALS += "恋慕 "
						SIF TALENT:LOOP_CHR:公衆便所
							LOCALS += "公衆便所 "
						PRINTFORM 好感度:{CFLAG:LOOP_CHR:(FLAG:1004),5,LEFT}  屈服度:{CFLAG:LOOP_CHR:屈服度,5,LEFT} %LOCALS%
					;浮気公認
					CASE 3
						PRINTFORM 浮気公認:{TALENT:LOOP_CHR:浮気公認}
					;子宮内精液量
					CASE 4
						精子量 = 0
						FOR LOOP_I, 0, 301
							REPEAT 5
								IF TA:LOOP_CHR:LOOP_I:COUNT > 0
									精子量 += TA:LOOP_CHR:LOOP_I:COUNT
								ENDIF
							REND
						NEXT
						IF TALENT:LOOP_CHR:妊娠
							LOCALS:1 = 孕
						ELSEIF CFLAG:LOOP_CHR:卵子生存日数 == 1
							LOCALS:1 = 排
						ELSEIF TALENT:LOOP_CHR:危険日
							LOCALS:1 = 危
						ELSE
							LOCALS:1 = 　
						ENDIF
						PRINTFORM 子宮内精液量:{精子量,5}/{CFLAG:LOOP_CHR:子宮内体積,5}
						PRINTFORM  %LOCALS:1%
					;弱味
					CASE 10
						VARSET LOCALS
						SIF GETBIT(CFLAG:LOOP_CHR:(FLAG:1004),0) 
							LOCALS += "ぱんつを没収された　"
						SIF GETBIT(CFLAG:LOOP_CHR:(FLAG:1004),1) 
							LOCALS += "情事を見られた　"
						PRINTFORM %LOCALS%
				ENDSELECT
		ENDSELECT

		IF ARG == 2
			IF LOOP_CHR == ASSI
				PRINT 　　　　　
			ELSEIF ISASSI:LOOP_CHR == 1
				PRINT [元助手] 
			ELSE
				PRINT [未経験] 
			ENDIF
		ENDIF
		IF TALENT:LOOP_CHR:アイドル==1
			PRINTFORM ﾗﾝｸ:%IDOLRANK(CFLAG:LOOP_CHR:アイドルランク)%
			PRINTFORM ﾌｧﾝ:{CFLAG:LOOP_CHR:ファン人数}人
			CALL IDOLSTATAS(LOOP_CHR)
		ENDIF
		PRINTL
	NEXT
	PRINTL 

;ステータス表示
@SHOW_CHARADATA
	R = 0
	CALL LIFE_LIST
;	PRINTLC  [2000]基礎表示切替
;	PRINTLC  [3000]能力表示切替
;	PRINTLC  [4000]性技表示切替
;	PRINTLC  [5000]刻印表示切替
;	PRINTLC  [6000]経験表示切替
;	PRINTLC  [7000]宝珠表示切替
;	PRINTLC  [8000]その他表示切替
	PRINTFORML  [1000]戻る

	$INPUT_LOOP
	INPUT
	IF RESULT == 1000
		RETURN 0
	ELSEIF RESULT == 2000 || RESULT == 3000 || RESULT == 4000 || RESULT == 5000 || RESULT == 6000 || RESULT == 7000 || RESULT == 8000
		CALL SHOW_CHARADATA2, RESULT
		RESTART
	ELSEIF RESULT < 0 || RESULT >= CHARA_ACTIVE
		GOTO INPUT_LOOP
	ENDIF
	CALL PRINT_STATE(RESULT, 0, 1)
	RESTART

@SHOW_CHARADATA2, ARG
	VARSET LOCAL, 0
	LOCAL:999 = ARG/1000
	DRAWLINE
	DRAWLINE

	SELECTCASE LOCAL:999
		;基礎表示選択
		CASE 2
			PRINTLC  [   0] 体力
			PRINTLC  [   1] 気力
			LOCAL:0 = 1
			LOCAL:1 = 1
			LOCAL:998 = 1
		;能力表示選択(性技は別）
		CASE 3
			FOR LOCAL:998,0,40
				SIF !STRLENS(ABLNAME:(LOCAL:998))
					CONTINUE
				PRINTFORMLC  [{LOCAL:998,4}] %ABLNAME:(LOCAL:998),12%
				LOCAL:(LOCAL:998) = 1
			NEXT
		;性技表示選択
		CASE 4
			FOR LOCAL:998,50,60
				SIF !STRLENS(ABLNAME:(LOCAL:998))
					CONTINUE
				PRINTFORMLC  [{LOCAL:998,4}] %ABLNAME:(LOCAL:998),12%
				LOCAL:(LOCAL:998) = 1
			NEXT
		;刻印表示選択
		CASE 5
			FOR LOCAL:998,0,5
				SIF !STRLENS(MARKNAME:(LOCAL:998))
					CONTINUE
				PRINTFORMLC  [{LOCAL:998,4}] %MARKNAME:(LOCAL:998),12%
				LOCAL:(LOCAL:998) = 1
			NEXT
		;経験表示選択
		CASE 6
			FOR LOCAL:998,0,100
				SIF !STRLENS(EXPNAME:(LOCAL:998))
					CONTINUE
				PRINTFORMLC  [{LOCAL:998,4}] %EXPNAME:(LOCAL:998),16%
				LOCAL:(LOCAL:998) = 1
			NEXT
		;宝珠表示選択
		CASE 7
			FOR LOCAL:998,0,22
				SIF !STRLENS(PALAMNAME:(LOCAL:998))
					CONTINUE
				PRINTFORMLC  [{LOCAL:998,4}] %PALAMNAME:(LOCAL:998),6%の珠
				LOCAL:(LOCAL:998) = 1
			NEXT
			PRINTLC  [ 100] 否定の珠
			LOCAL:100 = 1
			LOCAL:998 = 100
		;その他表示選択
		CASE 8
			PRINTLC  [   1]既成事実
			PRINTLC  [   2]好感度
			PRINTLC  [   3]浮気公認
			PRINTLC  [   4]子宮内精液量
			PRINTLC  [  10]弱味
			LOCAL:1 = 1
			LOCAL:2 = 1
			LOCAL:3 = 1
			LOCAL:4 = 1
			LOCAL:10 = 1
			LOCAL:998 = 10
	ENDSELECT
	PRINTL  [1000] 戻る
	$INPUT_LOOP
	INPUT
	IF RESULT == 1000
		RETURN 0
	ELSEIF RESULT < 0 || RESULT > LOCAL:998 || LOCAL:RESULT <= 0
		GOTO INPUT_LOOP
	ENDIF
	FLAG:1003 = LOCAL:999
	FLAG:1004 = RESULT


@CHARA_LIST
	LOCAL:2 = 0
	;アイテム番号(100 + 30N) + 1から30人ずつ表示（NはTFLAG:100）
	FOR LOCAL, TFLAG:100 * 30 + 1,TFLAG:100 * 30 + 30 + 1
		LOCAL:1 = LOCAL + 100
		SIF ITEMSTOCK(LOCAL:1) == 1
			CONTINUE
		LOCALS = [{LOCAL:1}]%ITEMNAME:(LOCAL:1)% (${ITEMPRICE:(LOCAL:1)})
		;売り切れ
		IF ITEMSTOCK(LOCAL:1) == 2
			LOCALS = [{LOCAL:1}]%ITEMNAME:(LOCAL:1)% (売り切れ)
			SETCOLOR 色設定_テキスト_無効
		ENDIF
		PRINTFORM %LOCALS,26,LEFT%
		RESETCOLOR
		LOCAL:2 += 1
		SIF LOCAL:2 % 3 == 0
			PRINTL
	NEXT

@CHARA_BUY2
	CALL CHARA_LIST
	SIF FLAG:1001 & 1
		PRINTL [400]ランダムキャラ($10)
	SIF FLAG:1001 & 2
		PRINTL [401]カスタムキャラ($10)
	DRAWLINE
	PRINT 　page
	PRINTV TFLAG:100
	SIF TFLAG:100
		PRINT 　[1]前のページ
	PRINTC [0]購入しない
	;上限300人
	SIF TFLAG:100 < 10 
		PRINTC [9]後のページ
	PRINTL 
	$INPUT_LOOP1
	INPUT
	LOCAL = RESULT

	IF LOCAL == 0
		TFLAG:100 = 0
		RETURN 0
	ELSEIF LOCAL == 1 && TFLAG:100
		TFLAG:100 -= 1
		RESTART
	ELSEIF LOCAL == 9 && TFLAG:100 < 10
		TFLAG:100 += 1
		RESTART
	ENDIF
	IF (LOCAL == 400 && FLAG:1001 & 1)|| (LOCAL == 401 && FLAG:1001 & 2)
		CALL CHARA_BUY_SELECT(LOCAL)
		SIF RESULT == 1
			RESTART
	;アイテムの奴隷枠は101〜399まで
	ELSEIF LOCAL < 101 || LOCAL > 400
		CLEARLINE 1
		GOTO INPUT_LOOP1
	ELSE
		SELECTCASE ITEMSTOCK(LOCAL)
			CASE 0
				CALL CHARA_BUY_SELECT(LOCAL)
				SIF RESULT == 1
					RESTART
			CASE 1
				RESTART
			CASE 2
				PRINTW 売切です
				RESTART
			CASE 3
				PRINTW 所持金が足りません
				RESTART
		ENDSELECT
	ENDIF

@CHARA_BUY_SELECT(ARG)
	CALL CHARA_MANUAL(ARG)
	PRINTL 
	CALL CHOICE("購入しますか", "はい", "いいえ")
	SIF RESULT
		RETURN 1
	CALL CHARA_BUY_AFTER(ARG)

@CHARA_BUY_AFTER(ARG)
	MONEY -= ITEMPRICE:ARG
	LOCAL = ARG - 100
	IF ARG == 401
		ADDVOIDCHARA
		CALL CUSTOM_CHARAMAKE(CHARA_ACTIVE-1)
	ELSE
		ADDCHARA LOCAL
	ENDIF
	PRINTFORMW %NAME:(CHARA_ACTIVE - 1)%を購入しました
	;キャラの購入はITEMSALESで管理する
	TARGET = CHARA_ACTIVE - 1
	SIF !TALENT:200
	ITEMSALES:ARG --
	;キャラの通し番号
	CFLAG:5 = CFLAG:MASTER:5 ++
	RETURN 0

;キャラ購入時に解説を入れる
@CHARA_MANUAL(ARG)
