;-------------------------------------------------------------------------------
;(訪問者宅にて)脱走する(拡張版)
; RETURN 0:コマンドの実行に失敗, 1:コマンドの実行に成功
; MASTER/TARGET/PLAYER/ASSIに影響されず呼び出したいので、中身は関数に切り出す
@COM461
	CALL COMMAND461(MASTER)
RETURN RESULT:0

;-------------------------------------------------------------------------------
;(訪問者宅にて)脱走する(拡張版)
; @return 0:コマンドの実行に失敗,1:コマンドの実行に成功
@COMMAND461(奴隷)
	#DIM 奴隷
	#DIMS コマンド,100
	#DIM コマンド数 = 0
	;未設定の場合初期位置にリセット
	IF CFLAG:奴隷:現在位置詳細 == 0
		CFLAG:奴隷:現在位置詳細 = 25
	ENDIF
	IF FLAG:訪問者の現在位置 != 場所_訪問者宅 && RAND:20 == 0
		CALL COMMAND461_01(奴隷)
		RETURN 1
	ENDIF
	コマンド数 = 0
	CALL COM461_MAP(奴隷)
	$COM461_INPUT_LOOP
	INPUT
	TRYCCALLFORM COM461_%TOSTR(RESULT:0,"00")%
	CATCH
		CLEARLINE 1
		GOTO COM461_INPUT_LOOP
	ENDCATCH
RETURN RESULT:0
RETURN 0

;-------------------------------------------------------------------------------
;訪問者の帰宅
;@return 1:xxx,0:xxx
@COMMAND461_01(奴隷)
	#DIM 奴隷
	FLAG:訪問者の現在位置 = 場所_訪問者宅
	SELECTCASE CFLAG:奴隷:現在位置詳細
		CASE 24,25,26,27
		PRINTFORMW %"どうやら、"+NTR_NAME(他人)+"が帰ってきたようだ……"%
	CASEELSE
		PRINTFORMW %"牢屋の外に出ている所を、帰ってきた"+NTR_NAME(他人)+"に見つかった！"%
		PRINTFORMW %CALLNAME:奴隷+"は牢屋に連れ戻され、お仕置きとして激しく犯される事になってしまった……"%
		;場所は牢屋に
		CFLAG:奴隷:現在位置詳細 = 25
		;鍵は全部かける
		FLAG:訪問者宅の施錠状態 ^= (訪問者宅_牢屋解錠済|訪問者宅_通路解錠済|訪問者宅_床扉解錠済)
		CFLAG:奴隷:NTR訪問者宅監禁状況 ^= (訪問者宅_牢屋解錠済|訪問者宅_通路解錠済|訪問者宅_床扉解錠済)
		CALL COM46x_V_3(奴隷)
	ENDSELECT
RETURN 1



;-------------------------------------------------------------------------------
;FIXME:関数の説明を書いてください
;@return 1:xxx,0:xxx
@COM461_MAP(奴隷)
	#DIM 奴隷
	#DIMS 地図,2,9
	#DIM 階層
	#DIM 行数
	#DIMS HTMLデータ
	#DIMS 自分位置色
	#DIMS 無効位置色
	;地上部分データ
	地図:01:00 = %"┏━┳┓─┏━━┓"%
	地図:01:01 = %"┃02┃　01　　03┃"%
	地図:01:02 = %"┣　┛　　　　　┃"%
	地図:01:03 = %"┃04│　　　　　┃"%
	地図:01:04 = %"┣　┓　05　　06┃"%
	地図:01:05 = %"┃　┃　　　　　┃"%
	地図:01:06 = %"┃07┃　┏　━━┫"%
	地図:01:07 = %"┃　┃08┃　09　┃"%
	地図:01:08 = %"┗━┻━┻━━━┛"%
	;地下部分データ
	地図:00:00 = %"　　　　┏━━━┓"%
	地図:00:01 = %"　　　　┃　　　┃"%
	地図:00:02 = %"┏━┳━┫　23　┃"%
	地図:00:03 = %"┃21：22：　　　┃"%
	地図:00:04 = %"┗━╋‥┻━┳━┛"%
	地図:00:05 = %"　　┃24　　┃　　"%
	地図:00:06 = %"　　┃　25　┃　　"%
	地図:00:07 = %"　　┃26　27┃　　"%
	地図:00:08 = %"　　┗━━━┛　　"%
	;地図描画
	;FIXME:手抜き。ちゃんとColorSettings.erhの値をそのうち変換すること。
	自分位置色 = %"#00FF00"%
	FOR 階層,VARSIZE("地図",0)-1,-1,-1
		HTMLデータ '= ""
		FOR 行数,0,VARSIZE("地図",1)
			FOR LOCAL:0,0,100
				TRYCCALLFORM %"COM461_"+TOSTR(LOCAL:0,"00")+"_ENABLE"%, 奴隷
					IF RESULT:0 && STRCOUNT(地図:階層:行数,TOSTR(LOCAL:0,"00")) > 0
						IF CFLAG:奴隷:現在位置詳細 == LOCAL:0
							地図:階層:行数 = %REPLACE(地図:階層:行数,TOSTR(LOCAL:0,"00"),"<button value='"+TOSTR(LOCAL:0,"00")+"'><font color='"+自分位置色+"'>"+TOSTR(LOCAL:0,"00")+"</font></button>")%
							地図:階層:行数 = %地図:階層:行数+"  "+"<button value='"+TOSTR(LOCAL:0,"00")+"'><font color='"+自分位置色+"'>"+TOSTR(LOCAL:0,"00")+":"+RESULTS:0+"</font></button>"%
						ELSE
							地図:階層:行数 = %REPLACE(地図:階層:行数,TOSTR(LOCAL:0,"00"),"<button value='"+TOSTR(LOCAL:0,"00")+"'>"+TOSTR(LOCAL:0,"00")+"</button>")%
							地図:階層:行数 = %地図:階層:行数+"  "+"<button value='"+TOSTR(LOCAL:0,"00")+"'>"+TOSTR(LOCAL:0,"00")+":"+RESULTS:0+"</button>"%
						ENDIF
					ELSE
						地図:階層:行数 = %REPLACE(地図:階層:行数,TOSTR(LOCAL:0,"00"),"  ")%
					ENDIF
				CATCH
				ENDCATCH
			NEXT
			HTMLデータ '= HTMLデータ+地図:階層:行数+"<br>"
		NEXT
		HTMLデータ '= "<font face='MS Gothic'>"+HTMLデータ+"</font>"
		HTML_PRINT HTMLデータ
	NEXT
RETURN 0
