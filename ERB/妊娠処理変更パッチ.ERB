
;妊娠処理変更パッチ======================================
;新規追加フラグ

;DA:X:Y = Xの子宮内にあるYの総精子量
;TA:X:Y:Z(0~4) = Xの子宮内にあるYの精子量　Zは振り分け番号
;TB:X:Y:Z(0~4) = Xの子宮内にあるZ番のYの精子の残り生存日数
;Yの値、訪問者は300,客は299,村人は298とする

;CFLAG:350 妊娠処理における妊娠確率の分母、便宜上"子宮内体積"と明記

;TFLAG:精子量 射精される精子量

;子宮内体積は体型により変化、幼児体型など通常より小さければ子宮内体積も小さくなり、妊娠しやすくなる
;　長身など体が大きければその逆となる
;妊娠確率は子宮内の総合精子量/子宮内体積
;射精される側(体型:普通)が排卵日の時に射精する側の精力が全快状態(1000)で膣内射精を一回行うと20％の確立で妊娠する



;====================================================

;PREGNASY_S.ERB 出産、成長・ここ〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜
@子宮内体積設定(奴隷)
	#DIM 奴隷
	;子宮内体積（体型により変化）
	IF TALENT:奴隷:体型 == -3
		CFLAG:奴隷:子宮内体積 = 1500
	ELSEIF TALENT:奴隷:体型 == -2
		CFLAG:奴隷:子宮内体積 = 3000
	ELSEIF TALENT:奴隷:体型 == -1
		CFLAG:奴隷:子宮内体積 = 4000
	ELSEIF TALENT:奴隷:体型 == 1
		CFLAG:奴隷:子宮内体積 = 6000
	ELSEIF TALENT:奴隷:体型 == 2
		CFLAG:奴隷:子宮内体積 = 8000
	ELSEIF TALENT:奴隷:体型 == 3
		CFLAG:奴隷:子宮内体積 = 12000
	;普通
	ELSE
		CFLAG:奴隷:子宮内体積 = 5000
	ENDIF


;SOURCE_SHOOT.ERB・NTR.ERB〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜
;処理上、CALL 精子取得 だと既に精力が減少してしまっているのでこっちで取得
;登録外のキャラもとりあえずこっちで取得
@精力取得(射精者)
	#DIM 射精者		; 射精したキャラ 998=訪問者

	;精子量取得
	;誰か（客）
	IF 射精者 == 人物_客 || 射精者 == 人物_村人
		IF RAND:100 == 1
			;稀に濃度の濃い奴
			TFLAG:精子量 = RAND(1000, 2000)
		ELSE
			TFLAG:精子量 = RAND(1, 200)
		ENDIF
	;訪問者
	ELSEIF 射精者 >= 人物_訪問者
		TFLAG:精子量 = RAND(150, 300)
	ELSE
		;減少する前の精力の値
		TFLAG:精子量 = BASE:射精者:精力
		;コマンド[挿入子宮口責め]以外は3割減少
		;TFLAG:50 == 7 特殊COM,挿入子宮口責め
		SIF TFLAG:特殊COM != 7
			TIMES TFLAG:精子量, 0.70
	ENDIF


@精子取得(射精者, 被害者)
	#DIM 射精者		;射精したキャラ 998=訪問者
	#DIM 被害者		;射精されたキャラ

	;村人を298,客を299,訪問者を300とする
	SIF 射精者 == 人物_訪問者
		射精者=他人
	;0420 射精先をNOで管理,よってTAなどの関数は,TA:(被害者の登録番号):加害者のNO番号	になる。
	SIF 射精者 < CHARA_ACTIVE
		射精者=(NO:射精者)
	;子宮内精子量増加（TFLAG:精子量の分だけ上昇）
	REPEAT 5
		;日数が0なら、精子量追加
		IF TB:被害者:射精者:COUNT == 0
			TA:被害者:射精者:COUNT += TFLAG:精子量
			TRYCALL 精子取得DEBUG(TFLAG:精子量, TA:被害者:射精者:COUNT, 被害者, 射精者, COUNT)
			BREAK
		ENDIF
	REND

;AFTERTRA.ERB〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜

;妊娠判定の前の処理
@精子処理_生存日数振り分け
	#DIM 母親候補	;チェック対象
	#DIM 父親候補	;チェック対象を孕ませる相手
	#DIM 総精子量	;チェック対象の子宮内精子量の合計

	FOR 母親候補, 0, CHARA_ACTIVE

		;男(ふたなりは除く)はスキップ
		SIF TALENT:母親候補:性別 == 2
			CONTINUE
		
		FOR 父親候補, 0, 1100
			;全キャラ回せば次に訪問者と客の振り分けを行うようにする
;			SIF 父親候補 == CHARA_ACTIVE
;				父親候補 = 精子主_スキップ
			
			総精子量 = 0
			REPEAT 5
				;日数フラグが0、精子量が０超なら日数セット
				IF TB:母親候補:父親候補:COUNT == 0 && TA:母親候補:父親候補:COUNT > 0
					TB:母親候補:父親候補:COUNT = 3
				;日数フラグが１、精子量が０超ならリセット
				ELSEIF TB:母親候補:父親候補:COUNT == 1 && TA:母親候補:父親候補:COUNT > 0
					TA:母親候補:父親候補:COUNT = 0
					TB:母親候補:父親候補:COUNT = 0
				;日数フラグが残っており、精子量が０超なら日数フラグ減少＋精子量20％減
				ELSEIF TB:母親候補:父親候補:COUNT > 0 && TA:母親候補:父親候補:COUNT > 0
					TIMES TA:母親候補:父親候補:COUNT, 0.80
					TB:母親候補:父親候補:COUNT --
				ENDIF
				総精子量 += TA:母親候補:父親候補:COUNT
			REND
			;現在の総精子量のセット
			DA:母親候補:父親候補 = 総精子量
			TRYCALL 精子処理DEBUG2(母親候補, 父親候補, 総精子量)
		
		NEXT
		
	NEXT

;受精関連（此処3つで一つ）-------------------------------
@妊娠処理変更パッチ
	#DIM 母親候補	; 母親候補(MASTERとキャラ)
	#DIM 父親候補	; 父親候補(MASTERときキャラと訪問者と客)
	#DIM 総精子量	; 母親候補の子宮内の総合精子量
	#DIM 精子種類	; 母親候補の子宮内の精子の種類数

	;対象１のチェック
	FOR 母親候補, 0, CHARA_ACTIVE
		;男(ふたなりは除く)はスキップ
		SIF TALENT:母親候補:性別 == 2
			CONTINUE
		;排卵日　ただしランダムで排卵日以外でも(1/10)
		SIF RAND:10==0
			GOTO SKIP_HAIRANBI
		SIF CFLAG:母親候補:卵子生存日数 != 1
			CONTINUE
		$SKIP_HAIRANBI
		;ピル使用中ならスキップ
		SIF CFLAG:母親候補:ピル使用 == 3
			CONTINUE
		SIF TALENT:母親候補:不妊
			CONTINUE
		;妊娠しない設定のときはスキップ
		SIF !GETBIT(FLAG:雑多設定,5)
			CONTINUE
		;0300 仮 所属していないキャラが妊娠すると面倒なのでスキップ
;		SIF !所属(母親候補) || !TALENT:母親候補:恋慕
;			CONTINUE
		総精子量 = 0
		精子種類 = 0
		;何種の精子が子宮内に有るかチェック
		FOR 父親候補, 0, 1100
			;全キャラ回せば(ry
	;		SIF 父親候補 == CHARA_ACTIVE
	;			父親候補 = 精子主_スキップ
			;訪問者での妊娠が OFF になっている場合は除外
			SIF 父親候補 >= 人物_訪問者 && !IS_NTR_PREGNANCY_ENABLE()
				BREAK

			;子宮内に父親候補の精子が有るか？
			IF DA:母親候補:父親候補 > 0
				;有れば総精子量に加算、種類数＋１
				総精子量 += DA:母親候補:父親候補
				精子種類 ++
			ENDIF
		NEXT
		
		TRYCALL 妊娠処理変更パッチDEBUG(母親候補, 精子種類)
		
		;1種でも精子が有るならチェックへ
		SIF 精子種類 > 0 
			CALL 受精チェック(母親候補, 総精子量, 精子種類)
	NEXT

;中出しされた精子量に応じて確率処理
@受精チェック(母親候補, 総精子量, 精子種類)
	#DIM 母親候補	; 対象
	#DIM 総精子量	; 子宮内の総合精子量
	#DIM 精子種類	; 何種類の精子が存在するか
	#DIM 当たり番号	; 受精させた精子番号
	;妊娠するかの判定
	当たり番号 = RAND:(CFLAG:母親候補:子宮内体積)
	;百発百中(デバッグ用)
	;当たり番号 = 1
	SIF TALENT:母親候補:繁殖母体==2
		当たり番号 = 1
	TRYCALL 受精チェックDEBUG(総精子量, 当たり番号)
	;誰かの精子が当たっていれば誰の子か判定
	IF 当たり番号 <= 総精子量
		CALL 誰の子(母親候補, 総精子量, 当たり番号, 精子種類)
	ENDIF

@誰の子(母親候補, 総精子量, 当たり番号, 精子種類)
	#DIM 母親候補	; 妊娠チェックの対象
	#DIM 総精子量	; 子宮内の総合精子量
	#DIM 当たり番号	; 受精させた精子番号
	#DIM 精子種類	; 何種類の精子が存在するか
	#DIM 父親候補	; 父親候補
	#DIM 持ち番号	; 父親候補の持ち番号
		
	持ち番号 = 0
	;対象２のチェック
	FOR 父親候補, 0, 1100
		;全キャラ回せば(ry
;		SIF 父親候補 == CHARA_ACTIVE
;			父親候補 = 精子主_スキップ
	
		;対象２の精子がなければ次へ
		SIF DA:母親候補:父親候補 <= 0
			CONTINUE
		;訪問者での妊娠が OFF になっている場合は除外
		SIF 父親候補 >= 人物_訪問者 && !IS_NTR_PREGNANCY_ENABLE()
			CONTINUE
		
		;精子量追加（前回の持ち番号の数値〜今回の持ち番号の数値-1までが現在の対象２の持ち番号）
		持ち番号 += DA:母親候補:父親候補
	
		TRYCALL 精子量追加DEBUG, 母親候補, 父親候補, 持ち番号, 当たり番号
	
		;持ち番号に当たり番号の番号があるか
		IF (持ち番号 - 1) >= 当たり番号
			;有れば対象２の子を孕む
			;訪問者
		
			CFLAG:母親候補:にんっしんっ = 父親候補+1
			
			BREAK
		ENDIF
	NEXT

	;複数人の精子が有るなら誰の子か分からなくする
	;@PREGNACY_S_BIRTHで-1000
	SIF 精子種類 > 1
		CFLAG:母親候補:にんっしんっ += 10000

	;妊娠が確定したのでいらないフラグをリセット
	CFLAG:母親候補:卵子生存日数 = 0
	CFLAG:母親候補:生理周期 = 0
	TALENT:母親候補:危険日 = 0

	;PRINT_STATE.ERB〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜〜
	;対象の子宮内の精子総量表示(予定)


;子供の成長状態表示
@子供の成長状態(ARG)
	#FUNCTIONS

	SELECTCASE CFLAG:ARG:成長度
		CASE 1
			RETURNF "<赤子>"
		CASE 2
			IF NO:ARG == 148
				RETURNF "<幼児>"
			ELSE
				RETURNF "<幼女>"
			ENDIF
		CASE 3
			RETURNF "<成長期>"
		CASEELSE
			RETURNF ""
	ENDSELECT


